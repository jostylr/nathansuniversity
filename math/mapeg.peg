// parser definition for mathpeg
// mainly parsing block structure


start = 
  wsn s : statement+ wsn
  {return  s; }
  
  
statement = 
  wsn b: block ws stEnd
  { return b;}
  / wsn e: expr ws stEnd
  { return e;}

stEnd = 
  [\n\r] 
  / ";" 
  / !.
  / & (rightBracket) 

block = 
  wsn lb : leftBracket wsn s: statement+ wsn rb : rightBracket ws
  {return {tag: "block", left: lb, stmnt : s, right : rb};}

leftBracket = 
  "(" / "[" / "{" / "\(" / "\[" / "\{" / ":" [\n]

rightBracket = 
  ")" / "]" / "}" / "\)" / "\]" / "\}" / "." [\n]  

// expr sucks up the ending absolute value if it is allowed in symbol
abshelper = 
  ws s: symbol  e: expr
  {return [s].concat(e);}

abs = 
  wsn l : "|"+ w : (word / block ) wsn a: abshelper? wsn r : "|"+ wsn
  {return {tag : "abs", left: l.length, right : r.length, expr :  [w].concat(a)};}


// expressions -- no operator processing
expr = 
 e: wordws+
 {return e;}
  
wordws = 
  ws w: word ws
  {return w;}

word = 
   abs 
  / identifier
  / number
  / string
  / special
  / block
  / symbol
  
special = 
  ws "'" i :[^ \n\r\t]+ & ([ \n\r\t] / !.) ws
  {return {tag : "tok", value : i.join('')}; }

symbol = 
  !( leftBracket / rightBracket/ "|" ) a: [^A-Za-z0-9 \r\n\t]+
  {return {tag : "tok", value : a.join('')} ;}

string = 
  '"' str : [^"]+ '"' 
  { return {tag : "string", value : str.join("")} }

identifier = 
  first: [a-zA-Z] rest : [a-zA-Z0-9]*
  {return {tag: "tok", value : first + rest.join("")};}
  
  
number = 
  scientific
  / decimal
  / integer

scientific = 
  deci : decimal "E" exp : integer
  {return {tag : "sci", deci : deci, exp : exp};}

decimal = 
  int : integer "." right : [0-9]+
  {return {tag: "decimal", left: int , right : right}; }  
  
integer = 
  n : "-" ? left : [0-9]+ 
  {return {tag : "int", neg : (n === "-" ? true : false), value : left}; }  

  
wsn = [ \n\r\t]*

ws = [ \t]*
